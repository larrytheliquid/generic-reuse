module ListVecReuse.
import Unit.
import Sigma.
import IIdMapping.
import Eq.
import Id.
import FixIndM.
import IFixIndM.
import FogFix.
import EnrFix.
import Nat.
import ListF.
import List.
import VecF.
import Vec.

vf2lf ◂ ∀ A : ★. ∀ X : Nat ➔ ★. ∀ Y :  ★. 
  Π c : ∀ n : Nat. Id · (X n) · Y.
  ∀ n : Nat. Id · (VecF · A · X n) · (ListF · A · Y) 
  = Λ A. Λ X. Λ Y. λ c. elimVecF · A · X
  · (λ n : Nat. IdCod · (VecF · A · X n) · (ListF · A · Y))
  (pairId · (VecF · A · X zero) · (ListF · A · Y) -(nilVF · A · X) (nilLF · A · Y) β)
  (Λ m. λ a. λ x. pairId · (VecF · A · X (suc m)) · (ListF · A · Y)
    -(consVF · A · X -m a x) (consLF · A · Y a (elimId · (X m) · Y (c -m) x)) β).

v2l ◂ ∀ A : ★. ∀ n : Nat. Id · (Vec · A n) · (List · A)
  = Λ A. Λ n. ifix2fix · Nat · (VecF · A) · (ListF · A)
  (imapV · A) (imapL · A) (vf2lf · A) -n.
v2l! ◂ ∀ A : ★. ∀ n : Nat. Vec · A n ➔ List · A
  = Λ A. Λ n. elimId · (Vec · A n) · (List · A) (v2l · A -n).

lf2vf ◂ ∀ A : ★. ∀ Y : ★. ∀ X : Nat ➔ ★.
  Π r : Y ➔ Nat. 
  Π c : IdDep · Y · (λ y : Y . X (r y)).
  IdDep · (ListF · A · Y) · (λ xs : ListF · A · Y . VecF · A · X (lenAlgM · A · Y r xs))
  = Λ A. Λ Y. Λ X. λ r. λ c. elimListF · A · Y
  · (IdDepCod · (ListF · A · Y) · (λ xs : ListF · A · Y . VecF · A · X (lenAlgM · A · Y r xs)))
  (pairId · (ListF · A · Y) · (VecF · A · X zero) -(nilLF · A · Y) (nilVF · A · X) β)
  (λ a. λ y. pairId · (ListF · A · Y) · (VecF · A · X (lenAlgM · A r (consLF · A · Y a y)))
    -(consLF · A · Y a y) (consVF · A · X -(r y) a (elimIdDep · Y · (λ y : Y . X (r y)) c y)) β).
 
l2v ◂ ∀ A : ★. IdDep · (List · A) · (λ xs : List · A. Vec · A (len · A xs))
  = Λ A. fix2ifix · (ListF · A) · Nat · (VecF · A)
  (imapL · A) (imapV · A) (lenAlgM · A) (lf2vf · A).
l2v! ◂ ∀ A : ★. Π xs : List · A. Vec · A (len · A xs)
  = Λ A. elimIdDep · (List · A) · (λ xs : List · A. Vec · A (len · A xs)) (l2v · A).

lf2vfP ◂ ∀ A : ★. ∀ Y : ★. ∀ X : Nat ➔ ★.
  Π c1 : Id · Y · (List · A).
  Π c2 : IdDep · Y · (λ y : Y. ∀ n : Nat. Eq · Nat n (len · A (elimId · Y · (List · A) c1 y)) ➾ X n).
  IdDep · (ListF · A · Y) · (λ ys : ListF · A · Y. ∀ n : Nat.
    Eq · Nat n (len · A (inL (elimId · (ListF · A · Y) · (ListF · A · (List · A)) (imapL · A · Y · (List · A) c1) ys)))
    ➾ VecF · A · X n)
  = Λ A. Λ Y. Λ X. λ c1. λ c2. elimListF · A · Y
  · (IdDepCod · (ListF · A · Y) · (λ ys : ListF · A · Y. ∀ n : Nat.
    Eq · Nat n (len · A (inL (elimId · (ListF · A · Y) · (ListF · A · (List · A)) (imapL · A · Y · (List · A) c1) ys)))
    ➾ VecF · A · X n))
  (pairId · (ListF · A · Y) · (∀ n : Nat. {n ≃ len nilL} ➾ VecF · A · X n) -(nilLF · A · Y) (Λ n. Λ q. ρ q - (nilVF · A · X)) β)
  (λ a. λ y. pairId · (ListF · A · Y) · (∀ n : Nat. {n ≃ len (consL a y)} ➾ VecF · A · X n)
    -(consLF · A · Y a y)
    (Λ n. Λ q. ρ q - (consVF · A · X -(len · A (elimId · Y · (List · A) c1 y)) a
      (elimIdDep · Y · (λ y' : Y. ∀ n' : Nat. {n' ≃ len y'} ➾ X n')
        c2 y -(len · A (elimId · Y · (List · A) c1 y)) -β)
    )) β
  ).

l2vP ◂ ∀ A : ★. IdDep · (List · A) · (λ xs : List · A. ∀ n : Nat. Eq · Nat n (len · A xs) ➾ Vec · A n)
  = Λ A. fix2ifixP · (ListF · A) · Nat · (VecF · A) (imapL · A) (imapV · A)
  · (λ n : Nat. λ xs : List · A. Eq · Nat n (len · A xs))
  (lf2vfP · A).
l2vP! ◂ ∀ A : ★. Π xs : List · A. ∀ n : Nat. Eq · Nat n (len · A xs) ➾ Vec · A n
  = Λ A. elimIdDep · (List · A) · (λ xs : List · A. ∀ n : Nat. Eq · Nat n (len · A xs) ➾ Vec · A n) (l2vP · A).

