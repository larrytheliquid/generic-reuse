module Nat.
import Eq.

NatC ◂ ★ = ∀ C : ★. C ➔ (C ➔ C) ➔ C.
zeroC ◂ NatC = Λ C. λ cZ. λ cS. cZ.
sucC ◂ NatC ➔ NatC = λ n. Λ C. λ cZ. λ cS. cS (n · C cZ cS).

NatI ◂ NatC ➔ ★ = λ n : NatC. ∀ P : NatC ➔ ★. P zeroC ➔ (∀ n' : NatC. P n' ➔ P (sucC n')) ➔ P n.
Nat ◂ ★ = ι n : NatC. NatI n.
zero ◂ Nat = [zeroC, Λ P. λ pZ. λ pS. pZ].
suc ◂ Nat ➔ Nat = λ n. [sucC n.1, Λ P. λ pZ. λ pS. pS -n.1 (n.2 · P pZ pS)].

toNat ◂ NatC ➔ Nat = λ n. n · Nat zero suc.
toNatId ◂ Π n : Nat. Eq · Nat (toNat n.1) n = λ n. n.2
  · (λ nC : NatC. HEq · Nat · NatC (toNat nC) nC)
  β (Λ mC. λ ih. ρ+ ih - β).

elimNat ◂ ∀ P : Nat ➔ ★. P zero ➔ (∀ n : Nat. P n ➔ P (suc n)) ➔ Π n : Nat. P n
  = Λ P. λ pZ. λ pS. λ n.
  ρ ς (toNatId n) -
  (n.2 · (λ nC : NatC. P (toNat nC))
    pZ (Λ mC. λ ih. pS -(toNat mC) ih)).

add ◂ Nat ➔ Nat ➔ Nat = λ m. λ n. m.1 · Nat n suc.

