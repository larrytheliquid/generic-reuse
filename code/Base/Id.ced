module Id.
import Eq.
import Sigma.

IdDepCod ◂ Π A : ★. Π B : A ➔ ★. A ➔ ★ = λ A : ★. λ B : A ➔ ★.
 λ a : A. Sigma · (B a) · (λ b : B a. { b ≃ a }).

IdDep ◂ Π A : ★. Π B : A ➔ ★. ★ = λ A : ★. λ B : A ➔ ★. 
 Π a : A. IdDepCod · A · B a.

elimIdDep~ ◂ ∀ A : ★ . ∀ B : A ➔ ★ . IdDep · A · B ➾ Π a : A . B a =
  Λ A . Λ B . Λ c . λ a .
  subst · (B a) · A -(proj1 · (B a) · (λ b : (B a). {b ≃ a}) (c a)) a -(proj2 · (B a) · (λ b : (B a). {b ≃ a}) (c a)) .

elimIdDep ◂ ∀ A : ★ . ∀ B : A ➔ ★ . IdDep · A · B ➔ Π a : A . B a =
  Λ A . Λ B . λ c . elimIdDep~ · A · B -c.

elimIdDepId ◂ ∀ A : ★ . ∀ B : A ➔ ★ . ∀ q : IdDep · A · B .
  {elimIdDep · A · B q ≃ (λ x . x)}
  = Λ A. Λ B. Λ q. β .

intrIdDep ◂ ∀ X : ★. ∀ Y : X ➔ ★. Π f : (Π x : X. Y x). (Π z : X. {f z ≃ z}) ➔ IdDep · X · Y 
 = Λ X. Λ Y. λ if. λ p. λ a. pair · (Y a) · (λ y : (Y a). {y ≃ a}) (if a) (p a).

pairIdDep ◂ ∀ A : ★. ∀ B : A ➔ ★. ∀ a : A.
  Π b : B a. { b ≃ a } ➔ IdDepCod · A · B a
  = Λ A. Λ B. Λ a. λ b. λ q.
  pair · (B a) · (λ x : B a. { x ≃ a }) b q .

IdCod ◂ Π A : ★. Π B : ★. A ➔ ★ = λ A : ★. λ B : ★.
 IdDepCod · A · (λ _ : A. B).

Id ◂ Π A : ★. Π B : ★. ★ = λ A : ★. λ B : ★. IdDep · A · (λ _ : A. B).

elimId~ ◂ ∀ A : ★ . ∀ B : ★ . Id · A · B ➾ Π a : A . B =
  Λ A . Λ B . elimIdDep~ · A · (λ a : A. B) .

elimId ◂ ∀ A : ★ . ∀ B : ★ . Id · A · B ➔ Π a : A . B =
  Λ A . Λ B . λ c . elimId~ · A · B -c.

intrId ◂ ∀ X : ★. ∀ Y : ★. Π f : X ➔ Y. (Π z : X. {f z ≃ z}) ➔ Id · X · Y 
 = Λ X. Λ Y. λ if. λ p. λ a. pair · Y · (λ y : Y. {y ≃ a}) (if a) (p a).

trivId ◂ ∀ A : ★. Id · A · A = Λ A. λ a. pair · A · (λ a1 : A. {a1 ≃ a}) a β.

pairId ◂ ∀ A : ★. ∀ B : ★. ∀ a : A.
  Π b : B. { b ≃ a } ➔ IdCod · A · B a
  = Λ A. Λ B. pairIdDep · A · (λ a : A. B) .
