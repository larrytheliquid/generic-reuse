module ReuseForgetful.

import Id.
import IIdMapping.
import IFixIndM.
import IInductionM.
import Sigma.
import Unit.
import UnitLift.
import FixIndM.

Conv ◂ Π I : ★. Π J : ★. (I ➔ J) ➔ (J ➔ ★) ➔ (I ➔ ★)
 = λ I : ★. λ J : ★. λ f : I ➔ J. λ jX : J ➔ ★. λ i : I . jX (f i).

ifix2jfix ◂ ∀ I : ★. ∀ F : (I ➔ ★) ➔ (I ➔ ★). ∀ J : ★. ∀ G : (J ➔ ★) ➔ (J ➔ ★).
  Π imapF : IIdMapping · I · F.
  Π imapG : IIdMapping · J · G.
  Π f : I ➔ J. 
  Π c : ∀ X : I ➔ ★.  ∀ Y : J ➔ ★. (∀ i : I . Id · (X i) · (Y (f i))) ➔ ∀ i : I. Id · (F · X i) · (G · Y (f i) ).
  ∀ i : I. Id · (IFixIndM · I · F imapF i) · (IFixIndM · J · G imapG (f i)) =
  Λ I. Λ F. Λ J. Λ G. 
  λ imapF. λ imapG.
  λ f. λ ifg. Λ i. λ v.  induction · I · F -imapF ·  
  (λ ii : I. λ y : IFixIndM · I · F imapF ii. Sigma · (IFixIndM · J · G imapG (f ii)) · (λ x : (IFixIndM · J · G imapG (f ii)).  {x ≃ y}))
  (Λ R. Λ c. λ ih. Λ i. λ gr. 
        pair · (IFixIndM · J · G imapG (f i)) · (λ x : IFixIndM · J · G imapG (f i) . ({x ≃ iinFixIndM gr})) 
     (iinFixIndM · J · G -imapG -(f i) 
         (elimId · (F · (Conv · I · J f · (IFixIndM · J · G imapG)) i) · (G · (IFixIndM · J · G imapG) (f i)) 
           (ifg · (λ i : I. IFixIndM · J · G imapG (f i)) · (IFixIndM · J · G imapG) 
               (Λ i : I. trivId · (IFixIndM · J · G imapG (f i))) -i)  
           (elimId · (F · R i) · (F · (Conv · I · J f · (IFixIndM · J · G imapG)) i ) 
           (imapF · R · (Conv · I · J f · (IFixIndM · J · G imapG)) ih -i) gr))) 
     β) -i v.

ifix2fix ◂ ∀ I : ★. ∀ F : (I ➔ ★) ➔ (I ➔ ★). ∀ G : ★ ➔ ★. 
  Π imapF : IIdMapping · I · F.
  Π imapG : IdMapping · G.
  Π c : ∀ X : I ➔ ★.  ∀ Y : ★. (∀ i : I . Id · (X i) · Y) ➔ ∀ i : I. Id · (F · X i) · (G · Y).
  ∀ i : I. Id · (IFixIndM · I · F imapF i) · (FixIndM · G imapG)
  = Λ I. Λ F. Λ G. λ imapF. λ imapG. 
  λ ifg. Λ i. λ v. ifix2jfix · I · F · Unit · (UnitF · G) imapF (uimapF · G imapG) (λ _. unit) 
  (Λ X. Λ Y. λ f. Λ i. λ a. pair · (UnitF · G · Y unit ) · (λ b : UnitF · G · Y unit . { b ≃ a }) 
   (elimId · (F · X i) · (G · (Y unit) ) (ifg · X · (Y unit) f -i) a) β) -i v.
