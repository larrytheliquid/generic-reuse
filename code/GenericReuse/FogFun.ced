module FogFun.
import Unit.
import Id.
import Aux.

allPi2piP' ◂ ∀ I : ★. ∀ X : I ➔ ★. ∀ X' : Π i : I. X i ➔ ★.
  ∀ Y : ★. ∀ Y' : Y ➔ ★. ∀ P : Y ➔ ★. 
  Π r : Y ➔ I.
  Π c1 : IdDep · Y · (λ y : Y. P y ➾ X (r y)).
  Π c2 : Π y : Y. ∀ p : P y. Id · (X' (r y) (elimIdDep · Y · (λ y : Y. P y ➾ X (r y)) c1 y -p)) · (Y' y).
  Id · (∀ i : I. Π x : X i. X' i x) · (Π y : Y. P y ➾ Y' y)
  = Λ I. Λ X. Λ X'. Λ Y. Λ Y'. Λ P. λ r. λ c1. λ c2. λ f. pairId
  · (∀ i : I. Π x : X i. X' i x) · (Π y : Y. P y ➾ Y' y) -f
  (λ y. Λ p. elimId · (X' (r y) (elimIdDep · Y · (λ y : Y. P y ➾ X (r y)) c1 y -p)) · (Y' y)
  (c2 y -p)
  (f -(r y) (elimIdDep · Y · (λ y : Y. P y ➾ X (r y)) c1 y -p))
  ) β.

allPi2pi' ◂ ∀ I : ★. ∀ X : I ➔ ★. ∀ X' : Π i : I. X i ➔ ★. ∀ Y : ★. ∀ Y' : Y ➔ ★.
  Π r : Y ➔ I.
  Π c1 : IdDep · Y · (λ y : Y. X (r y)).
  Π c2 : Π y : Y. Id · (X' (r y) (elimIdDep · Y · (λ y : Y. X (r y)) c1 y)) · (Y' y).
  Id · (∀ i : I. Π x : X i. X' i x) · (Π y : Y. Y' y)
  = Λ I. Λ X. Λ X'. Λ Y. Λ Y'. λ r. λ c1. λ c2.
  insertPiUnit · (∀ i : I. Π x : X i. X' i x) · Y · Y' (
  allPi2piP' · I · X · X' · Y · Y' · (λ _ : Y. Unit) r
  (skipPremDep · Y · Unit · (λ y : Y. X (r y)) c1)
  (λ y. Λ u. c2 y
  )).

allArr2arr' ◂ ∀ I : ★. ∀ X : I ➔ ★. ∀ X' : I ➔ ★. ∀ Y : ★. ∀ Y' : ★.
  Π r : Y ➔ I.
  Π c1 : IdDep · Y · (λ y : Y. X (r y)).
  Π c2 : Π y : Y. Id · (X' (r y)) · Y'.
  Id · (∀ i : I. X i ➔ X' i) · (Y ➔ Y')
  = Λ I. Λ X. Λ X'. Λ Y. Λ Y'.
  allPi2pi' · I · X · (λ i : I. λ x : X i. X' i) · Y · (λ y : Y. Y').

allArr2arr ◂ ∀ I : ★. ∀ X : I ➔ ★. ∀ X' : I ➔ ★. ∀ Y : ★. ∀ Y' : ★.
  Π r : Y ➔ I.
  Π c1 : IdDep · Y · (λ y : Y. X (r y)).
  Π c2 : ∀ i : I. Id · (X' i) · Y'.
  Id · (∀ i : I. X i ➔ X' i) · (Y ➔ Y')
  = Λ I. Λ X. Λ X'. Λ Y. Λ Y'. λ r. λ c1. λ c2.
  allArr2arr' · I · X · X' · Y · Y' r c1
  (λ y . c2 -(r y)).

allPi2pi ◂ ∀ I : ★. ∀ X : I ➔ ★. ∀ X' : Π i : I. X i ➔ ★. ∀ Y : ★. ∀ Y' : Y ➔ ★.
  Π r : Y ➔ I.
  Π c1 : IdDep · Y · (λ y : Y. X (r y)).
  Π c2 : ∀ i : I. Id · (X i) · Y.
  Π c3 : ∀ i : I. Π x : X i. Id · (X' i x) · (Y' (elimId · (X i) · Y (c2 -i) x)).
  Id · (∀ i : I. Π x : X i. X' i x) · (Π y : Y. Y' y)
  = Λ I. Λ X. Λ X'. Λ Y. Λ Y'. λ r. λ c1. λ c2. λ c3.
  allPi2pi' · I · X · X' · Y · Y' r c1
  (λ y. c3 -(r y) (elimIdDep · Y · (λ y2 : Y. X (r y2)) c1 y)).

