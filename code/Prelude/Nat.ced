module Nat.

cNat ◂ ★ = ∀ X : ★ . (X ➔ X) ➔ X ➔ X .

cZ ◂ cNat = Λ X . λ f . λ a . a .

cS ◂ cNat ➔ cNat = λ n . Λ X . λ f . λ a . f (n · X f a) .


cNatInductive ◂ cNat ➔ ★ = λ x : cNat . ∀ Q : cNat ➔ ★ . (∀ x : cNat . Q x ➔ Q (cS x)) ➔ Q cZ ➔ Q x.

Nat ◂ ★ = ι x : cNat . cNatInductive x.

Z ◂ Nat = [ cZ , Λ X . λ s . λ z . z ] .
S ◂ Nat ➔ Nat = λ n . [ cS n.1 , Λ P . λ s . λ z . s -n.1 (n.2 · P s z) ] .

zero ◂ Nat = Z.
suc  ◂ Nat ➔ Nat = S.

NatInd ◂ Π x : Nat . ∀ Q : Nat ➔ ★ . (∀ x : Nat . Q x ➔ Q (S x)) ➔ Q Z ➔ Q x =
  λ x . Λ Q . λ s . λ z . 
    x.2 · (λ x : cNat . ∀ X : ★ . (Π x' : Nat . {x ≃ x'} ➔ Q x' ➔ X) ➔ X)
     (Λ x' . λ ih . Λ X . λ c . ih · X (λ x'' . λ e . λ u . c (S x'') (ρ e - β) (s -x'' u)))
     (Λ X . λ c . c Z β z) · (Q x) (λ x' . λ e . λ u . ρ e - u).

add ◂ Nat ➔ Nat ➔ Nat = λ m . λ n . m.1 · Nat S n .

